#! /usr/bin/env janet

(import shlib)
(import sh)

(def hist-file (first (shlib/wordexp "~/.janetsh.hist")))
(def rc-file (first (shlib/wordexp "~/.janetsh.rc")))

(shlib/ln/history-set-max-len 512)
(shlib/ln/set-multiline true)
(try 
  (shlib/ln/history-load hist-file)
  ([e] nil))

(var *get-prompt*
  (fn get-prompt [p]
    (string (os/cwd) " " (parser/state p) "$ ")))

# Use a var so users can redefine this in rc file.
(var *get-completion*
  (fn get-completion
    [line lc]
    (var expand-idx 0)
    (def rline (string/reverse line))
    (when-let [idx (string/find " " rline)]
      (set expand-idx (- (length line) idx)))
    (var to-expand (string (string/slice line expand-idx) "*"))
    (each choice (sh/expand to-expand)
      (shlib/ln/add-completion lc (string line choice)))))

(defn getln [prompt buf]
  (when-let [ln (shlib/ln/get-line prompt *get-completion*)]
    (when (not= (length ln) 0)
      (shlib/ln/history-add ln))
    (buffer/push-string buf ln "\n")
    buf))

(defn want-implicit [buf p]
  (and (> (length buf) 1) (empty? (parser/state p)) (not= (buf 0) 40)))

(defn getchunk [buf p]
  (sh/update-all-jobs-status)
  (when (getln (*get-prompt* p) buf)
    (when (want-implicit buf p)
      (let [line (string buf)]
        (buffer/clear buf)
        (buffer/format buf "(sh/$? %s)\n" line)))
    buf))

(setdyn :pretty-format "%.40p")

# convenience bindings.
(def $$ :macro sh/$$)
(def $? :macro sh/$?)
(def $  :macro sh/$)
(def clear sh/clear)
(def do-lines sh/do-lines)
(def out-lines sh/out-lines)

# Load user rc script.
# The user can overwrite the prompt with whatever they want.
(when (os/stat rc-file)
  # FIXME - slurp loses file name.
  (eval-string (slurp rc-file)))

# This should be improved.
(cond
  (= (length process/args) 2) (repl getchunk nil (fiber/getenv (fiber/current)))
  (= (length process/args) 3) (import* (get process/args 2)) # Can we import the convenience macros into this import?.
  (error "expected 0 or 1 arguments"))

(sh/terminate-all-jobs)
(shlib/ln/history-save hist-file)

(sh/deinit)